apply plugin: 'java'

defaultTasks 'clean', 'build', 'copyTrainingPlans'

sourceCompatibility = 1.7
version = '1.0'

compileTestJava.options.encoding = 'utf-8'
compileJava.options.encoding = 'utf-8'

ext {
  releaseDir = "release"
  trainingPlanDir = "training-plan"
  trainingReleasePath = releaseDir + "/" + trainingPlanDir
}

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.swinglabs', name: 'swingx', version: '0.9.2'
  testCompile group: 'junit', name: 'junit', version: '4.10'
}

jar {
  destinationDir = file(releaseDir)
  manifest {
    attributes 'Manifest-Version': version, 'Main-Class': 'com.selesse.marathontrainer.Main'
  }

  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

jar.doLast {
  ant.mkdir(dir: trainingReleasePath)
}

task copyTrainingPlans(type: Copy, dependsOn: 'jar') {
  from trainingPlanDir
  into trainingReleasePath
}

clean.doLast {
  ant.delete(dir: releaseDir)
}
